include_guard(GLOBAL)

if (NOT DEFINED _HPE_HWY_TAG)
    set(_HPE_HWY_TAG "1.3.0")
endif ()

if (NOT TARGET hwy::hwy AND NOT TARGET Highway::hwy)
    find_package(highway CONFIG QUIET)

    set(_HWY_BASE "")
    if (TARGET hwy)
        set(_HWY_BASE hwy)
    elseif (TARGET highway)
        set(_HWY_BASE highway)
    elseif (TARGET hwy::hwy)
        get_target_property(_aliased hwy::hwy ALIASED_TARGET)
        if (_aliased)
            set(_HWY_BASE ${_aliased})
        endif ()
    endif ()
    if (_HWY_BASE)
        if (NOT TARGET hwy::hwy)
            add_library(hwy::hwy ALIAS ${_HWY_BASE})
        endif ()
        if (NOT TARGET Highway::hwy)
            add_library(Highway::hwy ALIAS ${_HWY_BASE})
        endif ()
    endif ()
endif ()

if (NOT TARGET Highway::hwy)
    include(FetchContent)

    set(HWY_ENABLE_TESTS OFF CACHE BOOL "" FORCE)
    set(HWY_ENABLE_EXAMPLES OFF CACHE BOOL "" FORCE)
    FetchContent_Declare(highway
        GIT_REPOSITORY https://github.com/google/highway.git
        GIT_TAG        ${_HPE_HWY_TAG}
        GIT_SHALLOW    TRUE
        FIND_PACKAGE_ARGS)
    FetchContent_MakeAvailable(highway)

    set(_HWY_BASE "")
    if (TARGET hwy)
        set(_HWY_BASE hwy)
    elseif (TARGET highway)
        set(_HWY_BASE highway)
    elseif (TARGET hwy::hwy)
        get_target_property(_aliased hwy::hwy ALIASED_TARGET)
        if (_aliased)
            set(_HWY_BASE ${_aliased})
        endif ()
    endif ()
    if (_HWY_BASE)
        if (NOT TARGET hwy::hwy)
            add_library(hwy::hwy ALIAS ${_HWY_BASE})
        endif ()
        if (NOT TARGET Highway::hwy)
            add_library(Highway::hwy ALIAS ${_HWY_BASE})
        endif ()
    endif ()
endif ()

function(use_highway target)
    if (NOT TARGET ${target})
        message(FATAL_ERROR "use_highway called with unknown target `${target}`")
    endif ()

    if (TARGET Highway::hwy)
        target_link_libraries(${target} PUBLIC Highway::hwy)

        target_compile_definitions(${target} PUBLIC HINAPE_HAVE_SIMD=1)
    else ()
        message(STATUS "Highway not available; proceeding without it for `${target}`")
    endif ()
endfunction()
